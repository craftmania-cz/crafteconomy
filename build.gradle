plugins {
    id "org.ajoberstar.grgit" version "1.7.0"
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

apply plugin: 'java'
apply plugin: 'maven-publish'

group 'cz.craftmania.crafteconomy'
def mainVersion = "1.5.1"

sourceCompatibility = 16
targetCompatibility = 16

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

clean { delete "./build" }

// Name-master-76735d.jar
version = mainVersion

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

task generateJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
}

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://jitpack.io' }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://raw.github.com/PyvesB/AdvancedAchievements/mvn-repo/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven {
        url "https://maven.pkg.github.com/craftmania-cz/craftcore"
        credentials {
            username = GPR_USER
            password = GPR_TOKEN
        }
    }
    maven {
        url "https://maven.pkg.github.com/craftmania-cz/craftlibs"
        credentials {
            username = GPR_USER
            password = GPR_TOKEN
        }
    }
}

dependencies {
    compileOnly fileTree(include: ['*.jar'], dir: 'libs') // Add everything in the lib folder.
    compileOnly ('org.spigotmc:spigot-api:1.16.2-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5'
    }

    // Craftmania plugins
    compileOnly 'cz.craftmania.craftcore:craftcore:2.6.1-beta'
    compileOnly 'cz.craftmania.craftlibs:craftlibs:1.1.8'

    // Other dependecies
    implementation 'com.zaxxer:HikariCP:3.4.5'
    compileOnly 'org.ajoberstar:grgit:1.7.0'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'

    // Commands
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
}

shadowJar {
    relocate 'co.aikar.commands', 'cz.craftmania.crafteconomy.acf'
    relocate 'co.aikar.locales', 'cz.craftmania.crafteconomy.locales'
}

build.dependsOn shadowJar

publishing {
    repositories {
        maven {
            name = "crafteconomy"
            url = uri("https://maven.pkg.github.com/craftmania-cz/crafteconomy")
            credentials {
                username = GPR_USER
                password = GPR_TOKEN
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
