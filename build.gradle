plugins {
    id "org.ajoberstar.grgit" version "1.7.0"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'cz.craftmania.crafteconomy'
def mainVersion = "1.5.0"

sourceCompatibility = 11
targetCompatibility = 11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

clean { delete "./build" }

// Name-master-76735d.jar
version = mainVersion

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

task compilePlugin(type: Jar) {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task generateJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
}

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://packages.craftmania.cz/repository/craftmania/"}
    maven { url "https://packages.craftmania.cz/repository/third-party/"}
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://jitpack.io' }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://raw.github.com/PyvesB/AdvancedAchievements/mvn-repo/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    compileOnly fileTree(include: ['*.jar'], dir: 'libs') // Add everything in the lib folder.
    compileOnly ('org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5'
    }

    // Craftmania plugins
    compileOnly 'cz.craftmania.craftcore:craftcore:2.4.0'
    compileOnly 'cz.craftmania.craftlibs:craftlibs:1.1.8'

    // Other dependecies
    compile 'com.zaxxer:HikariCP:3.4.5'
    compileOnly 'org.ajoberstar:grgit:1.7.0'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'com.zrips:cmi:8.6.21.3'

    // Commands
    compile 'co.aikar:acf-paper:0.5.0-SNAPSHOT'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

shadowJar {
    relocate 'co.aikar.commands', 'cz.craftmania.crafteconomy.acf'
    relocate 'co.aikar.locales', 'cz.craftmania.crafteconomy.locales'
}

build.dependsOn shadowJar

publishing {
    repositories {
        maven {
            name = "crafteconomy"
            url = uri("https://maven.pkg.github.com/craftmania-cz/crafteconomy")
            credentials {
                username = GPR_USER
                password = GPR_TOKEN
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
