plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

apply plugin: 'java'
apply plugin: 'maven-publish'

group 'cz.craftmania.crafteconomy'
def mainVersion = "2.1.0-scheduler-beta"

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

clean { delete "./build" }

// Name-master-76735d.jar
version = mainVersion

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

task generateJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
}

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://jitpack.io' }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven {
        url "https://gitlab.com/api/v4/groups/craftmania/-/packages/maven"
        name "GitLab"
        credentials(HttpHeaderCredentials) {
            name = 'Private-Token'
            value = GITLAB_TOKEN
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }

}

dependencies {
    compileOnly fileTree(include: ['*.jar'], dir: 'libs') // Add everything in the lib folder.
    compileOnly ('org.spigotmc:spigot-api:1.16.2-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5'
    }

    // Craftmania plugins
    compileOnly 'cz.craftmania.craftcore:craftcore:2.6.2.1'
    compileOnly 'cz.craftmania.craftlibs:craftlibs:1.1.9'
    implementation 'cz.craftmania:craftactions:1.2-beta.1'

    // Other dependecies
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation group: 'org.quartz-scheduler', name: 'quartz', version: '2.3.2'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly group: 'me.clip', name: 'placeholderapi', version: '2.10.9'

    // Commands
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
}

shadowJar {
    baseName = 'crafteconomy'
    classifier = null
    version = this.version

    relocate 'co.aikar.commands', 'cz.craftmania.crafteconomy.acf'
    relocate 'co.aikar.locales', 'cz.craftmania.crafteconomy.locales'
}

publishing {
    repositories {
        maven {
            url "https://gitlab.com/api/v4/projects/33533197/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = GITLAB_TOKEN
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

build.dependsOn shadowJar
